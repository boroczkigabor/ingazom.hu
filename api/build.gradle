plugins {
    id 'java'
    id 'org.openapi.generator' version '4.3.0'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
}

group parent.group
version parent.version

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    def spring_version = "2.1.8.RELEASE"
    def swagger_version = "2.9.2"

    compileOnly "org.springframework.boot:spring-boot-starter-web:$spring_version"
    compile "io.springfox:springfox-swagger2:$swagger_version"
    compile "io.springfox:springfox-swagger-ui:$swagger_version"
    compile "com.github.joschi.jackson:jackson-datatype-threetenbp:2.6.4"
    compile "org.openapitools:jackson-databind-nullable:0.2.1"
}

sourceSets {
    main {
        java {
            srcDirs += "$buildDir/generated/java"
        }
    }
}

openApiValidate {
    inputSpec = "$projectDir/src/main/resources/api-backend.yaml"
    recommend = true
}

task openApiGenerateSpringServer(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "spring"
    group = "openapi tools"
    inputSpec = "$projectDir/src/main/resources/api-backend.yaml".toString()
    outputDir = "$buildDir/generated/java".toString()
    apiPackage = "org.atos.commutermap.network.service"
    invokerPackage = "org.atos.commutermap.network.service.invoker"
    modelPackage = "org.atos.commutermap.network.service.model"
    configOptions = [
            dateLibrary: "java8",
            interfaceOnly: "true"
    ]
}

task openApiGenerateJavascriptClient(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "javascript"
    group = "openapi tools"
    inputSpec = "$projectDir/src/main/resources/api-backend.yaml".toString()
    outputDir = "$buildDir/generated/js".toString()
    apiPackage = "org.atos.commutermap.network.service"
    invokerPackage = "org.atos.commutermap.network.service.invoker"
    modelPackage = "org.atos.commutermap.network.service.model"
}

build {
    dependsOn('openApiGenerateSpringServer')
    dependsOn('openApiGenerateJavascriptClient')
}
